
% --- Simple 2D EPR assembly geometry for Serpent tutorial

/************************
 * Material definitions *
 ************************/

% --- 5.0 wt-% enriched UO2
%     Temperature is set to 950 K

mat Fuel  -10.97 tmp 950.0 rgb 255 255 150 burn 1
92235.09c       -0.04644492
92238.09c       -0.83505247
 8016.09c       -0.11850261

% --- Cladding material Zircaloy-4
%     [Composition from PNNL-15870, Rev. 1]

mat Zircaloy4 -6.56000E+00 tmp 610
 8016.03c  -1.19276E-03
24050.03c  -4.16117E-05
24052.03c  -8.34483E-04
24053.03c  -9.64457E-05
24054.03c  -2.44600E-05
26054.03c  -1.12572E-04
26056.03c  -1.83252E-03
26057.03c  -4.30778E-05
26058.03c  -5.83334E-06
40090.03c  -4.97862E-01
40091.03c  -1.09780E-01
40092.03c  -1.69646E-01
40094.03c  -1.75665E-01
40096.03c  -2.89038E-02
50112.03c  -1.27604E-04
50114.03c  -8.83732E-05
50115.03c  -4.59255E-05
50116.03c  -1.98105E-03
50117.03c  -1.05543E-03
50118.03c  -3.35688E-03
50119.03c  -1.20069E-03
50120.03c  -4.59220E-03
50122.03c  -6.63497E-04
50124.03c  -8.43355E-04

% --- Graphite consisting solely of carbon-12
%     Flagged as bound scatterer with the "moder"-card

mat graphite -1.719 moder graphiteLib 6012 rgb 100 250 50
 6012.03c          1.00

% --- Thermal scattering libraries for graphite at room temperature

therm graphiteLib grj3.00t


% --- Coolant is water with 650 ppm soluble boric acid added
%     The temperature of water is 583 K
%     Density is calculated based on a pressure of 15.5 MPa
%     Hydrogen is flagged as a bound scatterer with the "moder"-card

mat water -0.70602 tmp 583 moder lwtr 1001 rgb 200 200 255
O-16.03c   3.330861e-01
H-1.03c    6.663259e-01
B-10.03c   7.186970e-05
B-11.03c   2.892846e-04

% --- Define thermal scattering libraries associated with hydrogen in light water
%     As there are no readymade thermal scattering libraries for 583 K
%     We will tell Serpent to interpolate using two bounding libraries:
%     -lwj3.11t (H-1 in light water at 574 K)
%     -lwj3.13t (H-1 in light water at 624 K)
%     See also: http://montecarlo.vtt.fi/download/SSS_THERMAL.pdf

therm lwtr 583 lwj3.11t lwj3.13t

/************************
 * Geometry definitions *
 ************************/

% --- Normal fuel rod 

pin FF
Fuel       0.3975
void       0.4125
Zircaloy4  0.4750
water


% --- Empty instrumentation thimble

pin ii
water       0.5725
Zircaloy4   0.6125
water

% --- Empty control rod channel

pin cc
water       0.5725
Zircaloy4   0.6125
water

% --- Empty lattice position (just water)

pin ww
water

% --- Pin lattice definition, name of the lattice "lat1"
%     Lattice type 1 (square lattice)
%     Lattice centered at 0.0 0.0
%     19 x 19 lattice elements (17x17 fuel rods + 1 layer of water)
%     Lattice pitch 1.26 cm
%     We'll utilize our knowledge of the 1/8 symmetry so that
%     we'll only have to type in 1/8th of the assembly

lat lat1 1 0.0 0.0 19 19 1.26
ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww
ww ww ww ww ww ww ww ww ww FF FF FF FF FF FF FF FF FF ww
ww ww ww ww ww ww ww ww ww FF FF FF FF FF FF FF FF ww ww
ww ww ww ww ww ww ww ww ww cc FF FF cc FF FF FF ww ww ww
ww ww ww ww ww ww ww ww ww FF FF FF FF FF cc ww ww ww ww
ww ww ww ww ww ww ww ww ww FF FF FF FF FF ww ww ww ww ww
ww ww ww ww ww ww ww ww ww cc FF FF cc ww ww ww ww ww ww
ww ww ww ww ww ww ww ww ww FF FF FF ww ww ww ww ww ww ww
ww ww ww ww ww ww ww ww ww FF FF ww ww ww ww ww ww ww ww
ww ww ww ww ww ww ww ww ww ii ww ww ww ww ww ww ww ww ww
ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww
ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww
ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww
ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww
ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww
ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww
ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww
ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww
ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww


% --- Tell Serpent to apply a symmetry to the lattice "lat1"
%     Symmetry axis is the z-axis (3)
%     Symmetry boundary condition is reflective (2)
%     Symmetry axis is located at coordinates (0.0 0.0)
%     Symmetry covers an angle starting from 270 degrees
%     and extending for 45 degrees

set usym lat1 3 2 0.0 0.0 270 45


% --- A square surface around the fuel assembly

surf s1 sqc  0.0 0.0 10.752

% --- Cell c1 belongs to the fuel assembly universe, is filled with the lattice lat1
%     and covers everything inside surface s1

cell c1 FA fill lat1     -s1

% --- Cell c2 belongs to the fuel assembly universe, is defined as an "outside" cell
%     and covers everything outside surface s1

cell c2 FA outside          s1


% --- lattice lat2 of fuel assemblies

lat lat2 1 0.0 0.0 19 19 21.504
ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww
ww ww ww ww ww ww ww ww ww FA FA FA ww ww ww ww ww ww ww
ww ww ww ww ww ww ww ww ww FA FA FA FA FA ww ww ww ww ww
ww ww ww ww ww ww ww ww ww FA FA FA FA FA FA FA ww ww ww
ww ww ww ww ww ww ww ww ww FA FA FA FA FA FA FA ww ww ww
ww ww ww ww ww ww ww ww ww ww ww FA FA FA FA ww ww ww ww
ww ww ww ww ww ww ww ww ww ww ww ww FA FA ww ww ww ww ww
ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww
ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww
ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww
ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww
ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww
ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww
ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww
ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww
ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww
ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww
ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww
ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww ww

set usym lat2 3 2 0.0 0.0 270 45

% --- A cylinder around the core

surf s2 cylz 0.0 0.0 200

% --- Cylinders to describe graphite reflector

surf s3 cylz 0.0 0.0 200
surf s4 cylz 0.0 0.0 220 

% --- Cell c3 belongs to the base universe 0, is filled with lattice lat2
%     and covers everything inside surface s2

cell c3 0 fill lat2      -s2
cell c6 0 water           s2 -s3

% --- Cell c4 belongs to base universe 0, creates graphite reflector ring

cell c4 0 graphite        s3 -s4

% --- Cell c5 belongs to the base universe 0, is defined as an "outside" cell
%     and covers everything outside surface s2

cell c5 0 outside         s4

/******************
 * Run parameters *
 ******************/

% --- Assembly linear power is 1100 MWe/300 cm = 3.67 MW/cm (template: 420 kW)

set power 3670000

% --- Boundary condition (1 = black, 2 = reflective, 3 = periodic)

%set bc 2

% --- Neutron population: 10000 neutrons per cycle, 1000 active / 200 inactive cycles

set pop 10000 1000 200

% --- XY-plot (3), which is 700 by 700 pixels and covers the whole geometry (now -100<(x,y)<100)

plot 3 700 700 0.0 -250 250 -250 250

% --- XY-meshplot (3), which is 700 by 700 pixels and covers the whole geometry

mesh 3 700 700


set lost -1

PIN POWER DISTRIBUTION

% --- Detector that calculates the pin power distribution in the assembly: 
%     Name of the detector is pinpowers.
%     The detector uses response number -8 (fission heat deposition)
%     of the material at the interaction site (keyword: void).
%     The detector has 17 bins in the x-direction between -10.71 cm and 10.71 cm
%     The detector has 17 bins in the y-direction between -10.71 cm and 10.71 cm

det pinpowers dr -8 void dx -10.71 10.71 17 dy -10.71 10.71 17


/***************************************
 * Settings for the burnup calculation *
 ***************************************/

% --- Burnup points for the burnup calculation
%     (in days, would be MWd/kgU if using butot instead of daytot)
%     Too long intervals between the burnup points will decrease the
%     accuracy of the burnup calculation, especially during the burn-out 
%     of burnable absorber 

% template:
%dep butot 0.1  0.5  1  3  5  7  9  11  13  15  17  19

dep daytot 0.1 1 5 10 15 20 25 30 35 40 45 50 55 60 65 70 75 80 85 90 95 100 110 120 130 140 150 200 250 300 350 400 450 500 600 700 800 900 1000 1100 1200 1300 1400 1500 1600 1700 1800 1900 2000 2500 3000 3500 4000 4500 5000 5500 6000 6500 7000 7500


% --- Material division for burnup calculation
%     Treat different pins of fuelNoGad as separate depletion zones (sep 1)

div Fuel  sep 1

% --- Material division for burnup calculation
%     Treat different pins of fuelYesGad as separate depletion zones (sep 1)
%     additionally divide each of those fuel pellets into 10 equal volume rings
%     between radial coordinates of 0.0 and 0.3975


set mcvol 10000000

% --- Nuclide inventory: these nuclides will be included in the
%     depletion output file burn_dep.m. The list can be changed
%     after the simulation has concluded if needed. Then you can run
%     sss2 -rdep burn
%     which will make Serpent re-read the inventory and re-produce the
%     depletion output file.

set inventory
 922350
 942390
 641550
 641570

% --- Use predictor corrector method for the depletion solution
%     leli: Linear extrapolation on predictor
%           Linear interpolation on corrector
%     10 10: 10 substeps on predictor, 10 substeps on corrector

set pcc leli 10 10

% --- Decay data library needs to be specified for burnup calculations

%set declib "sss_jeff31.dec"

set acelib "/opt/serpent/xsdata/sss_endfb7u.xsdata"
set declib "/opt/serpent/xsdata/sss_endfb7.dec"
set nfylib "/opt/serpent/xsdata/sss_endfb7.nfy"


/*************************************************
 * Settings for the critical boron concentration *
 *************************************************/
% 20 - additional 20 inactive cycles 
% 1.005  - required multiplication factor 
% 2 - number of nuclides which concentration is searched
% 50100 & 50110 - boron 10 and 11 
% 1 - number of materials which consists absorbers 
% water - name of material which contains absorbers

set iter nuc 20 1.005 2 50100 50110 1 water

